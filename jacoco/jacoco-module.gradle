apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.12'
}

def exclusionPatterns = [
        '**/devtoy/common/**',
        '**/config/**',
        '**/exception/**',
        '**/aspect/**',
        '**/*Config.java'
]

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(true)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: exclusionPatterns)
                })
        )
    }
}

tasks.named('test', Test).configure { testTask ->
    testTask.finalizedBy(tasks.named("jacocoTestReport"))
}

sonar {
    properties {
        property("sonar.java.binaries", layout.buildDirectory.dir("classes").get().asFile.absolutePath)
        property("sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml").get().asFile.absolutePath)
        property("sonar.exclusions", exclusionPatterns)
    }
}
